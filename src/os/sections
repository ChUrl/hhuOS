ENTRY(startup)
OUTPUT_FORMAT(elf32-i386)

SECTIONS
 {
   . = 0xC0100000;     /* Startadresse des Systems */

   .text : AT (ADDR(.text) - 0xC0000000)
    {
      *(".text")
      *(".text.*")
      *(".text$")
      *(".init")
      *(".fini")
      *(".gnu.linkonce.*")
    }

    .init_array : AT (ADDR(.init_array) - 0xC0000000)
    {
       PROVIDE_HIDDEN (__init_array_start = .);
       KEEP (*(SORT(.init_array.*)))
       KEEP (*(.init_array))
       KEEP (*(".ctors"))
       KEEP (*(".ctor"))
       PROVIDE_HIDDEN (__init_array_end = .);
    }

    .fini_array : AT (ADDR(.fini_array) - 0xC0000000)
    {
       PROVIDE_HIDDEN (__fini_array_start = .);
       KEEP (*(SORT(.fini_array.*)))
       KEEP (*(.fini_array))
       KEEP (*(".dtors"))
       KEEP (*(".dtor"))
       PROVIDE_HIDDEN (__fini_array_end = .);
    }

   .data ALIGN(0x1000): AT (ADDR(.data) - 0xC0000000)
    {
      *(".data")
      *(".data$")
      *(".rodata")
      *(".rodata.*")
      *(".got")
      *(".got.plt")
      *(".eh_frame")
      *(".eh_fram")
      *(".jcr")
      *(".note.*")
    }

   .bss : AT (ADDR(.bss) - 0xC0000000)
    {
      ___BSS_START__ = .;
      *(".bss")
      *(".bss.*")
      ___BSS_END__ = .;
    }

/*
   /DISCARD/ :
    {
      *(".note")
      *(".comment")
      *(".debug_line")
      *(".debug_info")
      *(".debug_abbrev")
      *(".debug_aranges")
      *(".debug_ranges")
      *(".debug_str")
    }
*/
}
